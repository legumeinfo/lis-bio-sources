buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
        }
    }
    dependencies {
        classpath group: 'org.intermine', name: 'plugin', version: System.getProperty("imVersion")
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.6.2"
    }
}

ext {
    // set here to make available to individual bio-sources
    bioVersion = System.getProperty("bioVersion")
    imVersion = System.getProperty("imVersion")
}

subprojects {
    group = 'org.intermine'
    version = '5.1.0.4'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'dbmodel'
    apply plugin: 'biosource-dbmodel'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://www.ebi.ac.uk/Tools/maven/repos/content/groups/ebi-repo/'
        }
        maven {
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
        }
    }

    configurations {
        bioModel
        mergeModel
    }
    
    dependencies {
        compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.17.1'
        compile group: 'org.intermine', name: 'bio-core', version: System.getProperty("bioVersion")
        compile group: 'org.intermine', name: 'intermine-resources', version: System.getProperty("imVersion")
        compile group: 'org.intermine', name: 'intermine-model', version: System.getProperty("imVersion")
        compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
        testCompile group: 'junit', name: 'junit', version: '4.8.2'
        testCompile group: 'org.intermine', name: 'intermine-integrate', version: System.getProperty("imVersion"), classifier: "testClasses"
        testCompile group: 'xmlunit', name: 'xmlunit', version: '1.0'
        mergeModel group : 'org.intermine', name: 'intermine-objectstore', version: System.getProperty("imVersion")
        // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
        compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
        // https://mvnrepository.com/artifact/org.apache.commons/commons-text
        compile group: 'org.apache.commons', name: 'commons-text', version: '1.4'
        // https://mvnrepository.com/artifact/net.sourceforge.owlapi/owlapi-api
        compile group: 'net.sourceforge.owlapi', name: 'owlapi-api', version: '5.1.17'
        // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-yaml
        compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.13.1'
    }

    dbModelConfig {
        objectStoreName = "os.bio-test"
        modelName = "genomic"
        defaultInterminePropertiesFile = "default.intermine.bio-test.properties"
    }

    task copyMineProperties(type: Copy) {
        doLast {
            from "${System.env.HOME}/.intermine/intermine-bio-test.properties"
            into sourceSets.main.output.resourcesDir
            rename { fileName -> fileName.replace('intermine-bio-test.properties', 'intermine.properties') }
            inputs.sourceFiles.stopExecutionIfEmpty()
        }
    }
    
    jar {
        exclude "intermine.properties"
    }

    tasks.withType(Test) {
        scanForTestClasses = false
        include '**/*Test.class'
    }

    test {
        ignoreFailures = true
        forkEvery = 1
    }

    artifactory {
        contextUrl = 'https://oss.jfrog.org/artifactory/'
        resolve {
            repository {
                repoKey = 'libs-release'
            }
        }
        publish {
            repository {
                if (project.version.endsWith("-SNAPSHOT")) {
                    repoKey = 'oss-snapshot-local'
                } else {
                    repoKey = 'oss-release-local'
                }
                username = System.getenv('BINTRAY_USER')
                password = System.getenv('BINTRAY_KEY')
            }
            defaults {
                publishConfigs('archives')
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        pkg {
            repo = 'bio'
            name = project.name
            userOrg = user
            licenses = ['LGPL-2.1']
            vcsUrl = 'https://github.com/intermine/intermine.git'
        }
        configurations = ['archives']
    }

    tasks.compileTestJava.dependsOn ':bio-model:copyMineProperties'

    if (it.name != 'bio-source-lis-datastore') {
        dependencies {compile project(':bio-source-lis-datastore')}
    }

}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}
